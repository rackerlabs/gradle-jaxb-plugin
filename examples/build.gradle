buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    ////********************************************************************************
    //// use local artifacts
    ////--------------------------------------------------------------------------------
    //dependencies {
    //    classpath files('../build/libs/gradle-jaxb-plugin.jar')                          // gradle unversionedJar
    //    //classpath files("/${gradle.gradleHomeDir}/lib/plugins/gradle-jaxb-plugin.jar") // gradle installPlugin
    //    classpath 'com.google.inject:guice:3.0'
    //    classpath 'com.google.inject.extensions:guice-assistedinject:3.0'
    //}
    //********************************************************************************
    // use artifacts from the Gradle Plugin repository
    //--------------------------------------------------------------------------------
    dependencies {
        classpath 'gradle.plugin.org.openrepose:gradle-jaxb-plugin:2.4.0'
    }
    //********************************************************************************
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects { project ->
    if (project.name.endsWith("-schema") ||
            Boolean.valueOf(project.getProperties().getOrDefault('doJAXB', 'false'))) {
        apply plugin: 'org.openrepose.gradle.plugins.jaxb'

        dependencies {
            // TODO: jaxws 2.2.6 uses jaxb 2.2.5, but can't dL 2.2.5 from maven the pom is off
            jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41'
            jaxb 'com.sun.xml.bind:jaxb-impl:2.2.7-b41'
            jaxb 'javax.xml.bind:jaxb-api:2.2.7'
        }

        def generatedDir = "${project.buildDir}/generated-sources/xjc"

        jaxb {
            xjc {
                destinationDir = "$generatedDir"
            }
        }

        apply plugin: 'scala'

        dependencies {
            // 54.4. Dependency management
            compile 'org.scala-lang:scala-library:2.11.1'
            compile 'com.typesafe.scala-logging:scala-logging-slf4j_2.11:2.1.2'
            // 7.2. Declaring your dependencies
            testCompile group: 'junit', name: 'junit', version: '4.11'
        }

        sourceSets {
            main {
                scala {
                    srcDirs += "$generatedDir"
                }
            }
        }
    }
}

tasks.withType(ScalaCompile) {
    // 54.9. Compiling in external process
    configure(scalaCompileOptions.forkOptions) {
        memoryMaximumSize = '1g'
    }
    // 54.10. Incremental compilation
    scalaCompileOptions.useAnt = false
}
